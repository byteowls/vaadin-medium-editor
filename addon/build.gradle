plugins {
    id "com.jfrog.bintray" version "1.5"
}
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'osgi'

sourceCompatibility = 1.7
targetCompatibility = 1.7

eclipse {
    project {
        name = 'vaadin-medium-editor-addon'
    }
}

dependencies {
    compile ("com.vaadin:vaadin-server:${vaadinVersion}")

    testCompile "junit:junit:4.12"
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

javadoc {
    // this fixes a javadoc issue with vaadin packaging which is quite uncommon
    options.addStringOption("sourcepath", "")
}

task createAddonPom {
    pom { artifactId = project.artifactId }.writeTo("${sourceSets.main.output.resourcesDir}/META-INF/maven/com.byteowls/mediumeditor/pom.xml")
}

jar {
    baseName = project.artifactId
    // Include sources
    sourceSets.main.java.srcDirs.each {
        from it
    }

    manifest {
        // the manifest of the default jar is of type OsgiManifest
        name = 'vaadin-medium-editor'
        instruction 'Import-Package', '!com.google.gwt.*', '!com.vaadin.client.*', '*'
        instruction 'Bundle-Vendor', 'Michael Oberwasserlechner'
        instruction 'Bundle-Description', 'Medium Editor wrapper'
        instruction 'Bundle-DocURL', 'https://github.com/moberwasserlechner/vaadin-medium-editor'
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

// ###################################
// ### Publish to local maven repo ###
// ###################################

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.artifactId

            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// ##########################
// ### Publish to bintray ###
// ##########################

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    // configurations = ['archives'] //When uploading configuration files
    // - OR -
    publications = ['mavenJava'] //When uploading Maven-based publication files

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload

    // Package configuration. The plugin will use the repo and name properties to check if the package already exists.
    // In that case, there's no need to configure the other package properties (like userOrg, desc, etc).
    pkg {
        repo = 'maven'
        name = 'vaadin-medium-editor'
        //userOrg = 'byteowls' //An optional organization name when the repo belongs to one of the user's orgs
        desc = 'Vaadin Add-on for Medium Editor Javascript library'
        websiteUrl = 'https://github.com/moberwasserlechner/vaadin-medium-editor'
        issueTrackerUrl = 'https://github.com/moberwasserlechner/vaadin-medium-editor/issues'
        vcsUrl = 'https://github.com/moberwasserlechner/vaadin-medium-editor.git'
        licenses = ['MIT']
        labels = ['vaadin', 'addon', 'medium-editor', 'inline editing']
        publicDownloadNumbers = true

        version {
            name = project.version //Bintray logical version name
            // desc = 'optional'
            vcsTag = project.version
        }
    }
}
